plugins {
	id 'application'
	id 'com.intershop.gradle.jaxb' version '7.0.0'
}

repositories {
	mavenCentral()
}

dependencies {
	implementation 'org.apache.commons:commons-csv:1.11.0'
	implementation 'org.apache.commons:commons-lang3:3.16.0'
	implementation 'org.eclipse.angus:jakarta.mail:2.0.3'
	implementation 'org.glassfish.jaxb:jaxb-runtime:4.0.5'
	implementation 'jakarta.activation:jakarta.activation-api:2.1.3'
	implementation 'jakarta.mail:jakarta.mail-api:2.1.3'
	implementation 'jakarta.xml.bind:jakarta.xml.bind-api:4.0.2'
}

group = 'mobi.emmons'
version = '0.0.1'

jaxb {
	javaGen {
		dmarc_stats {
			schema = file("${projectDir}/src/main/resources/dmarc.xsd")
			binding = file("${projectDir}/src/main/resources/binding.xjb")
			packageName = 'mobi.emmons.dmarc_stats.generated'
			strictValidation = true
		}
	}
}

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(21)
	}
}

tasks.withType(JavaCompile) {
	options.compilerArgs += '-Xlint:all'
}

testing {
	suites {
		test {
			useJUnitJupiter('5.11.0')	// org.junit.jupiter:junit-jupiter
			targets {
				all {
					testTask.configure { 
						systemProperty 'email-password', project.property('zoho.email.password')
					}
				}
			}
		}
	}
}

application {
	mainClass = 'mobi.emmons.dmarc_stats.App'
}

task runStats(type: JavaExec) {
	dependsOn 'classes'
	mainClass = 'mobi.emmons.dmarc_stats.App'
	classpath = sourceSets.main.runtimeClasspath
	//systemProperty 'portal.password', "$portalPassword"
	args [
		'message-store',
		'imappro.zoho.com',
		'ian@emmons.mobi',
		project.property('zoho.email.password'),
		'Zoho/DMARC',
	]
}
